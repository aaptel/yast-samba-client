/**
 * File:	clients/samba-client_auto.ycp
 * Package:	Configuration of samba-client
 * Summary:	Client for autoinstallation
 * Authors:	Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 *
 * This is a client for autoinstallation. It takes its arguments,
 * goes through the configuration and return the setting.
 * Does not do any changes to the configuration.
 */

/**
 * @param function to execute
 * @param map/list of samba-client settings
 * @return map edited settings, Summary or boolean on success depending on called function
 * @example map mm = $[ "FAIL_DELAY" : "77" ];
 * @example map ret = WFM::CallFunction ("samba-client_auto", [ "Summary", mm ]);
 */

{

textdomain "samba-client";

y2milestone("----------------------------------------");
y2milestone("Samba-client auto started");

import "Samba";
include "samba-client/wizards.ycp";

any ret = nil;
string func = "";
map param = $[];

/* Check arguments */
if(size(WFM::Args()) > 0 && is(WFM::Args(0), string)) {
    func = (string) WFM::Args(0);
    if(size(WFM::Args()) > 1 && is(WFM::Args(1), map))
	param = (map) WFM::Args(1);
}
y2debug("func=%1", func);
y2debug("param=%1", param);

/* Create a summary*/
if(func == "Summary") {
    ret = Samba::Summary();
}
/* ShortSummary is used by Users module*/
else if(func == "ShortSummary") {
    ret = Samba::ShortSummary();
}
/* Reset configuration */
else if (func == "Reset") {
    Samba::Import($[]);
    Samba::globals_configured = false;
    Samba::modified = false;
    ret = $[];
}
/* Change configuration (run AutoSequence) */
else if (func == "Change") {
    ret = SambaClientAutoSequence();
}
/* Import configuration */
else if (func == "Import") {
    ret = Samba::Import(param);
}
/* Return packages required */
else if (func == "Packages") {
    ret = Samba::AutoPackages();
}
/* Return actual state */
else if (func == "Export") {
    ret = Samba::Export();
}
/* Read current state */
else if (func == "Read") {
    import "Progress";
    Progress::off();
    ret = Samba::Read();
    Progress::on();
}
/* Write givven settings */
else if (func == "Write") {
    import "Progress";
    Progress::off();
    Samba::write_only = true;
    ret = Samba::Write();
    Progress::on();
}

/**
 * Return if configuration  was changed
 * return boolean
 */
else if (func == "GetModified") {
    ret = Samba::modified;
}

/**
 * Set modified flag
 * return boolean
 */
else if (func == "SetModified") {
    Samba::modified = true;
    ret = true;
}

/* Unknown function */
else {
    y2error("Unknown function: %1", func);
    ret = false;
}

y2debug("ret=%1", ret);
y2milestone("Samba-client auto finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
