/**
 * File:	clients/samba-client.ycp
 * Package:	Configuration of samba-client
 * Summary:	Main file
 * Authors:	Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 *
 * Main file for samba-client configuration. Uses all other files.
 */

{

/***
 * <h3>Configuration of the samba-client</h3>
 */

textdomain "samba-client";

import "CommandLine";
import "Report";
import "Require";
import "Samba";

include "commandline/commandline.ycp";

/**
 * Enable or disable winbind service (high-level)
 *
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
global define boolean WinbindEnableHandler( map options ) ``{
    // check the "command" to be present exactly once
    string command = CommandLine::UniqueOption( options, 
	["enable", "disable" ] );
    if( command == nil ) return false;

    return Samba::enableWinbind( command == "enable" );
}

/**
 * Check domain membership.
 * 
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
global define boolean DomainMemberHandler( map options ) ``{

    string domain = options["domain"]:nil;
    
    // validate the options
    if( domain == nil ) {
	// user must provide the domain name to be tested
	// error message for isdomainmember command line action 
	Report::Error( sformat( _("You must specify the name of a domain.") ) );
	return false;
    }
    
    boolean result = Samba::isDomainMember( domain );
    if( result == nil ) {
	// translators: error message for isdomainmember command line action 
	Report::Error( _("Cannot test domain membership.") );
	return false;
    }
    
    if( result ) {
	// translators: result message for isdomainmember command line action 
	CommandLine::Print( sformat( _("This machine is a member of '%1'"), domain ) );
    } else {
	// translators: result message for isdomainmember command line action 
	CommandLine::Print( sformat( _("This machine is not a member of '%1'"), domain ) );
    }
    
    return true;
}

/**
 * Join a domain.
 * 
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
global define boolean JoinDomainHandler( map options ) ``{
    string domain = options["domain"]:nil;
    
    // validate the options
    if( domain == nil ) {
	// must provide the domain name to be joined
	// error message for joindomain command line action 
	Report::Error( sformat( _("You must specify the name of a domain.") ) );
	return false;
    }
    
    string result = Samba::joinDomain( domain, options["user"]:nil, options["password"]:"" );
    if( result == nil ) {
	// translators: result message for joindomain command line action 
	CommandLine::Print( sformat( _("Domain '%1' was successfully joined."), domain ) );
	return true;
    } else {
	Report::Error( result );
	return false;
    }
}

/**
 * Change workgroup name.
 * 
 * @param options  a list of parameters passed as args
 * @return boolean true on success
 */
global define boolean ChangeConfiguration( map options ) ``{
	
    string value = options["workgroup"]:nil;
    if( value != nil ) Samba::setWorkgroup(value);

    return true;
}

/* The main () */
y2milestone ("----------------------------------------");
y2milestone ("Samba-client module started");

include "samba-client/wizards.ycp";

/* main ui function */
any ret = nil;

/* the command line description map */
map cmdline = $[
    "id" 		: "samba-client",
    // translators: command line help text for Samba client module
    "help" 		: _("Samba client configuration module.
See Samba documentation for details."),
    "guihandler" 	: ``(SambaClientSequence()),
    "initialize"	: ``(Samba::Read()),
    "finish"		: ``(Samba::Write()),
    "actions"		: $[
	"winbind" :$[
	    "handler"	: ``(WinbindEnableHandler()),
	    // translators: command line help text for winbind action
	    "help"	: _("Enable or disable the Winbind services (winbindd)")
	],
	"isdomainmember": $[
	    "handler"	: ``(DomainMemberHandler()),
	    // translators: command line help text for isdomainmember action
	    "help"	: _("Check, if this machine is a member of a domain")
	],
	"joindomain"	: $[
	    "handler"	: ``(JoinDomainHandler()),
	    // translators: command line help text for joindomain action
	    "help"	: _("Join this machine into a domain")
	],
	"configure"	: $[
	    "handler"	: ``(ChangeConfiguration()),
	    // translators: command line help text for configure action
	    "help"	: _("Change the global settings of the Samba server")
	]
    ],
    "options"		: $[
	"enable" 	:$[
	    // translators: command line help text for winbind enable option 
	    "help"	: _("Enable the service")
	],
	"disable"	:$[
	    // translators: command line help text for winbind disable option 
	    "help"	: _("Disable the service")
	],
	"domain"	:$[
	    // translators: command line help text for domain to be checked/joined 
	    "help"	: _("The name of a domain to join"),
	    "type"	: "string"
	],
	"user"		:$[
	    // translators: command line help text for joindomain user option 
	    "help"	: _("The user used for joining the domain. If omitted, YaST2 will
try to join the domain without specifying user and password."),
	    "type"	: "string"
	],
	"password"	:$[
	    // translators: command line help text for joindomain password option 
	    "help"	: _("The share used for the user when joining the domain"),
	    "type"	: "string"
	],
	"workgroup"	:$[
	    // translators: command line help text for the workgroup name option 
	    "help"	: _("The name of a workgroup"),
	    "type"	: "string"
	], 
    ],
    "mappings"		: $[
	"winbind" 	: [ "enable", "disable" ],
	"isdomainmember": [ "domain" ],
	"joindomain"	: [ "domain", "user", "password" ],
	"configure"	: [ "workgroup" ]
    ]
];

/* ensure samba package is installed */
if( !Require::RequireAndConflictTarget( ["samba-client"], [], 
    // notification about package needed 1/2
    _("<p>To configure the Samba client, the <b>%1</b> package must be installed.</p>") +
    // notification about package needed 2/2
    _("<p>Do you want to install it now?</p>")) )
{
    return `auto;
}

ret = CommandLineRun( cmdline );

y2debug("ret=%1", ret);

/* Finish */
y2milestone("Samba-client module finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
