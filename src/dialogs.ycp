/* File:	include/samba-client/dialogs.ycp
 * Package:	Configuration of samba-client
 * Summary:	Dialogs definitions
 * Authors:	Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 */

{

textdomain "samba-client";

import "Samba";
import "Label";
import "Popup";
import "Wizard";

include "samba-client/helps.ycp";
include "samba-client/routines.ycp";

/**
 * SAMBA workgroup dialog
 * @return dialog result
 */
global define symbol WorkgroupDialog () ``{

    /* Samba-client workgroup dialog caption */
    string caption = _("SAMBA Workgroup");
    
    Wizard::SetContentsButtons( caption, `HVSquash( `VBox(
	// frame label
	`Frame( _("Membership"), `VBox(
	    WorkgroupWidget(),
	    // checkbox label to enable winbind
	    `Left(`CheckBox( `id( `winbind ), _("Also &Use SMB Information for Linux Authentication"), Samba::winbind ) )
	) ) ) ), 
	HELPS["WorkgroupDialog"]:"", Label::BackButton(), Label::FinishButton() 
    );
    
    Wizard::ReplaceBackButton( `Empty() );
    
    any ret = nil;
    while(true) {

	ret = UI::UserInput();
	
	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(ReallyAbort()) break;
	    else continue;
	}
        else if(ret == `next || ret == `advanced ) {
	    Samba::setWorkgroup( UI::QueryWidget( `id( `workgroup ), `Value ) );
	    Samba::setWinbind( UI::QueryWidget( `id( `winbind ), `Value ) );
	    
	    // for domain ask to join
	    symbol workgroup_type = CheckWorkgroup( Samba::workgroup );
	    
	    if( Samba::winbind && workgroup_type == `workgroup ) {
		// translators: error message, winbind cannot provide user information taken from a workgroup, must be a domain
		// %1 is the workgroup name
		Popup::Error( sformat( _("Cannot use the workgroup 
'%1' for Linux authentication.

Enter name of a domain or disable
using SMB for Linux authentication."), Samba::workgroup ) );
		continue;
	    }

	    boolean in_domain = AskJoinDomain( Samba::workgroup, workgroup_type );
	    
	    // if we should start winbind, but it cannot work - not a domain, not joined
	    if( Samba::winbind && ! in_domain )
	    {
		// translators: error message, winbind cannot provide user information if the host is not in a domain
		Popup::Error( _("Host must be a member of a domain
for Linux authentication using SMB.

Join a domain or disable using SMB 
for Linux authentication.") );
		continue;
	    }
	    
            break;
        }
	else if(ret == `browse ) {
	
	    string new_workgroup = BrowseWorkgroups();
	    
	    if( new_workgroup != nil ) {
		// fill in the new workgroup name
		UI::ChangeWidget( `id(`workgroup), `Value, new_workgroup );
	    }
	    continue;
	}
    }
    
    Wizard::RestoreNextButton();
    Wizard::RestoreBackButton();
    
    return ret;
}

/* EOF */
}
